//httpFile 网络安装包
import fsys;
import zlib.unzip;
import inet.downBox;
namespace zlib.httpFile;

download = function(url,title="下载",saveDir,extraDir,saveName,parentForm){
	var tUrl = ..inet.url.split(url);
	if(!tUrl){
		error("错误的URL参数",2)
	}
	var tPath = ..io.splitpath(tUrl.path)
	
	saveDir = ..io.fullpath(saveDir : "~\download\" )
	..fsys.createDir(saveDir);
	
	if(!saveName){
		saveName = tPath.file;
	}
	if(!extraDir){
		extraDir = ..string.match(saveName,"(.+)\.[^\.]+$"  );
		if(!extraDir) extraDir = saveName + ".uncompress";
		extraDir = ..fsys.joinpath(saveDir,extraDir)
	}
	else {
		extraDir = ..io.fullpath(extraDir);
	} 
	
	var pathCabinet = ..fsys.joinpath(saveDir,saveName)
	var downBox = ..inet.downBox( parentForm,title,false);
	downBox.endProc = function(){  
		..win.delay(1000);
		downBox.text = "正在解压:" + saveName;
		..zlib.unzip.extract( pathCabinet,extraDir,
			function(fileName,extractPath,fileInfo,size,unitSize,unitName){
				downBox.labInfo.text = fileName;
				downBox.progress.stepIt(); 
				if(extractPath){ 
					return downBox.continueModal;
				}  
			}, ,
			function(numEntries){
				downBox.progress.hide = false;
				downBox.progress.setRange(1,numEntries);
				downBox.progress.pos = 0;
				downBox.progress.step = 1;
				downBox.labProgress.text = "正在解压缩,文件目录总数: " + numEntries;
			}
		) 
		owner.endModal(); 
	}
	downBox.download(url,pathCabinet ) 
	
	if(!downBox.complete){ 
		return;
	} 
	
	if( ! ..io.exist(pathCabinet,4) ){  
		return false; 
	} 
 
 	var fileRootDir;
 	..fsys.enum( extraDir, "*.*",
 		function(dir,filename,fullpath,findData){ 
 			if(filename){ 
 				fileRootDir = dir;
 	           	return false;
 			} 
 		}  
 	);
 	if(!fileRootDir)return;
 	
 	var setupFile = ..fsys.joinpath(fileRootDir,"setup.dl.aau")
	if( ..io.exist(setupFile) ){
		var handle = ..thread.create( setupFile ); 
		if( ..win.isClosed() ){
			..thread.wait(handle) 
		}
		else {
			..thread.waitOne(handle) 
		}  
	}
	return true; 		
}

/**intellisense(zlib)
httpFile = 网络安装包
httpFile.download(.(URL,对话框标题,下载目录,解压目录,存储文件名,父窗口) = 下载zip文件,除url以外，其他所有参数可选\n如果zip文件第一个包含文件的目录下包含 setup.dl.aau ,自动执行该文件安装
end intellisense**/

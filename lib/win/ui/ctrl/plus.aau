//plus 高级图像控件
import gdip.graphics;
import gdip.solidBrush;
import gdip.stringformat;
import gdip.font;
import win.ui.tracker;
import win.ui.ctrl.metaProperty;

namespace win.ui.ctrl;

class plus {
	ctor( parent,tParam ) {
		this.state = {};
		tParam.cls = __className;
		if(tParam.edge) tParam.exstyle |= 0x20000/*_WS_EX_STATICEDGE*/;  
		this.transparent = tParam.transparent;
		this.notify = tParam.notify;
		this.animation = tParam.animation; 
		this.color = 0; 
		
		this.bkTop = tParam.bkTop : 0;
		this.bkRight = tParam.bkRight : 0;
		this.bkBottom = tParam.bkBottom : 0;
		this.bkLeft = tParam.bkLeft : 0; 
		this.foreTop = tParam.foreTop : 0;
		this.foreRight = tParam.foreRight : 0;
		this.foreBottom = tParam.foreBottom : 0;
		this.foreLeft = tParam.foreLeft : 0; 
		this.paddingLeft = tParam.paddingLeft : 0;
		this.paddingRight = tParam.paddingRight : 0;
		this.paddingTop = tParam.paddingTop : 0;
		this.paddingBottom = tParam.paddingBottom : 0;  
		
		if( !tParam.font ) tParam.font = ::LOGFONT(name="MS Shell Dlg");
		elseif( tParam.font.name=="Tahoma" )  tParam.font.name="MS Shell Dlg";
		 
		if(!#tParam.repeat) tParam.repeat = "expand";
		if(!#tParam.foreRepeat) tParam.foreRepeat = "point"; 
		this[["(text)"]] = tParam.text;
		this.textRenderingHint =  3/*_GdipTextRenderingHintAntiAliasGridFit*/;
		this.clipBk = true;
	}; 
	redrawTransparent = function(){
		if( owner[["initializing"]] ) return;
		this.updateBitmapCache(true);
		if( ( this.transparent || this.isHyperlink() )  ){
 			var hParent = ..win.getParent(owner.hwnd);
 			var rc = ..win.getRect(owner.hwnd);
			::InvalidateRect( hParent,rc,1 ); 
			::UpdateWindow( hParent );   
			if( ..win.getStyle(hParent,0x2000000/*_WS_CLIPCHILDREN*/) ){
				::RedrawWindow(this[["hwnd"]],{},null,0x105 /*_RDW_INVALIDATE | _RDW_UPDATENOW*/ );
			}
		}
		else {  
			::RedrawWindow(this[["hwnd"]],{},null,0x105 /*_RDW_INVALIDATE | _RDW_UPDATENOW*/ );
		}
 	}; 
 	redraw = function( flag = 0x105 /*_RDW_INVALIDATE | _RDW_UPDATENOW */ ) {  
 		if( owner[["initializing"]] ) return;
 		this.updateBitmapCache();
		::RedrawWindow(owner[["hwnd"]],{},null,flag);
 	};
 	_preadjust = function(cx,cy){
 		this.updateBitmapCache(true);
 	};
	updateBitmapCache = function(erase){
		if( this.cacheBitmap ) { 
			..gdip.DeleteCachedBitmap(this.cacheBitmap);
			this.cacheBitmap = null; 
		}
		
		if( !..win.isWindow(this.hwnd) ) return;
		var foreground = this[["(foreground)"]];
		var background = this[["(background)"]]; 
		var rc = this.getClientRect();
		var width,height = rc.right - rc.left,rc.bottom - rc.top;
		var bmpMem = ..gdip.bitmap(width,height);
		if( !bmpMem ) return;
		
		var graphics = bmpMem.graphics;  
		graphics.smoothingMode = 4/*_GdipSmoothingModeAntiAlias*/
		graphics.compositingQuality = 2/*_GdipCompositingQualityHighQuality*/ 
		graphics.textRenderingHint = this.textRenderingHint;
		
		var winform = owner[["_parentForm"]];
		if( erase && !this.transparent && winform && this.clipBk && (!this.cacheOrphanWindowRect__ ) ){ 
			var hdc = graphics.getDc();
			winform.background.drawRect(hdc, ..win.getRect(owner.hwnd));
			graphics.releaseDc(hdc);
		}
		
		if( background ? background.isValid() ){  
			graphics.drawBackground(background,this["(repeat)"]
				,rc,this.bkTop,this.bkRight,this.bkBottom,this.bkLeft);
		}
		
		var cancel;
		if( this.onDrawContent ){
			cancel = this.onDrawContent(graphics,rc)
		}
		
		if( ! cancel ){ 
			var rcText;
			var text = this[["(text)"]];
			if( #text or this.onDrawString ){ 
				var x,y = this.paddingLeft,this.paddingTop;
				if( ( background ? this._tracker ? this._tracker.state.active ? (!this[["(progress-range)"]])  ) ) { x++;y++; };
				rcText = ::RECTF(x,y,width - x - this.paddingRight,height - y - this.paddingBottom);
			}
	
			if( foreground ? foreground.isValid()){   
				if( this["(foreRepeat)"] == "point"){
					if( background ?  #text ? this._tracker ? this._tracker.state.active ){
						rc.left = rc.left + 1;
						rc.top = rc.top + 1;
					} 
					graphics.drawImagePoint(foreground,rc,this.x,this.y)
				}
				else {    
				 	var rc = {left=this.paddingLeft;top=this.paddingTop;right=width - this.paddingRight;bottom=height- this.paddingBottom};
				 	
				 	if( this[["(progress-range)"]] && this[["(progress-pos)"]]!== null ){
				 		if( this[["(progress-pos)"]] ){
				 			var w = rc.right - rc.left;
				 			var h = rc.bottom - rc.top;
				 			var r = this[["(progress-pos)"]]/this[["(progress-range)"]];
				 			
				 			var path = this[["(progress-clip-path)"]];
				 			if( path ){
				 				path.reset(); 
				 				path.addPie( rc.left ,rc.top,w,h,90,360 * r );
				 				graphics.setClipPath(path,0/*_GdipCombineModeReplace*/);
				 			}
				 			else{
				 				if( this.mode = "trackbar" ){ 
				 					if( r > 1 ) r = 1;
				 					if( w > h ){
				 						var p = this.foreLeft + this.foreRight; 
				 						rc.right = rc.left + (w - p) * r + p;
				 						if(rcText) rcText.width = rc.right - rcText.x;
				 					}
				 					else {
				 						var p = this.foreTop + this.foreBottom;
				 						rc.top = rc.bottom - ( h - p) * r - p ;
				 						if(rcText) { rcText.y = rc.top; rcText.height = rc.bottom - rc.top; };
				 					} 
				 				}
				 				else{
				 					if( w > h ){
				 						w = ( w - this.foreLeft ) * r + this.foreLeft;
				 						graphics.setClipRect(rc.left,rc.top,w,h,0/*_GdipCombineModeReplace*/); 
				 						if(rcText) { rcText.width = ( rc.left + w ) - rcText.x };
				 					}
				 					else {
				 						var top = rc.bottom - ( h - this.foreBottom ) * r - this.foreBottom;
				 						graphics.setClipRect(rc.left,top,w,rc.bottom,0/*_GdipCombineModeReplace*/);
				 						if(rcText) { rcText.y =  top; rcText.height = rc.bottom - top; };
				 					} 
				 				}
				 	 		}
				 	 		
				 	 		graphics.drawBackground(foreground,this["(foreRepeat)"]
								,rc,this.foreTop,this.foreRight,this.foreBottom,this.foreLeft);
							graphics.resetClip()
				 		}
				 	}
				 	else {
				 	 	graphics.drawBackground(foreground,this["(foreRepeat)"]
							,rc,this.foreTop,this.foreRight,this.foreBottom,this.foreLeft);
				 	}
				}  
			}
			
			
			if( rcText ){  
				var strformat = ..gdip.stringformat();  
				strformat.align = __stringAlignment[ this.align : "center" ] 
				strformat.lineAlign = __stringAlignment[ this.valign : "center" ] 
				strformat.hotkeyPrefix = 1/*_GdipHotkeyPrefixShow*/; 
				strformat.flags = 0x800/*_GdipStringFormatFlagsMeasureTrailingSpaces*/ ;
				if(this.trimming!==null) strformat.trimming = this.trimming;

				if( !this.argbColor ) this.argbColor = ..gdi.rgbReverse( this.color  ) | ( 0xFF << 24 ) 
				var brush = ..gdip.solidBrush( this.argbColor );
				
				/**
	 			if( strformat.align = 1/*_GdipStringAlignmentCenter*/ && strformat.lineAlign = 1/*_GdipStringAlignmentCenter*/){
					var bound = graphics.measureString( text , font ,rcText, strformat,brush) 
					bound.x = rcText.x + (rcText.width - bound.width) /2;
					bound.y = rcText.y + (rcText.height - bound.height) /2;
					rcText = bound;
				} 
				**/
				if( this[["(font)"]] ) {
					if( !this.onDrawString ){
						graphics.drawString( text ,this[["(font)"]],rcText, strformat,brush);
					}
					else{
						this.onDrawString(graphics,text,this[["(font)"]],rcText, strformat,brush)	
					}
				}
				brush.delete();
			}
		} 
			
		graphics.delete();
		graphics = ..gdip.graphics(this) 
		this.cacheBitmap = graphics.createCachedBitmap(bmpMem);  
		graphics.delete();
		bmpMem.dispose();
		
		this.cacheBitmapWidth = width;
		this.cacheBitmapHeight = height;
		
		if( this.cacheOrphanWindowRect__ ){
		
		 	..gdi.layeredBuffer( this.hwnd,  
        		function( hdc,hMemDc,hMemBitmap,width,height ){
        			var graphics = ..gdip.graphics(hMemDc); 
					graphics.drawCachedBitmap(this.cacheBitmap,0,0);
					graphics.delete(); 
            	 	
        			..gdi.updateLayeredWindow( this.hwnd,hMemDc,::SIZE(width,height) ,..gdi.blendFunction()  ); 
        		}
        	)
		}
		return this.cacheBitmap;
	};
	onCreated = function () {  
	
		this.wndproc = { 
			[ 0xF/*_WM_PAINT*/] = function (hwnd, message, wParam, lParam) {   
			 
				..gdi.paint( hwnd,
					function( hdc,width, height,rcPaint,fErase,paintStruct ) {
						if( ! ( (width == this.cacheBitmapWidth) && (height == this.cacheBitmapHeight ) ) ){
							this.updateBitmapCache();
						}  
						if(this.cacheBitmap){
							..gdi.doubleBuffer( hdc,width,height,
							    function( hdc,hMemDc,hMemBitmap ){
							    	::BitBlt(hMemDc, 0, 0, width, height,hdc , 0, 0, 0xCC0020/*_SRCCOPY*/);
							        var graphics = ..gdip.graphics(hMemDc); 
									graphics.setClipRect( rcPaint.left,rcPaint.top,rcPaint.right,rcPaint.bottom,0/*_GdipCombineModeReplace*/);
									graphics.drawCachedBitmap(this.cacheBitmap,0,0);
							        ::BitBlt(hdc, 0, 0, width, height, hMemDc, 0, 0, 0xCC0020/*_SRCCOPY*/);
							        graphics.delete();
							    }
							) 
						}  
					}
				);  
					
				return 1; 
			}
			[ 0x14/*_WM_ERASEBKGND*/] = function (hwnd, uMsg, wParam, lParam) {
				return 0;
			} 
			[ 0xC/*_WM_SETTEXT*/ ] = function (hwnd, uMsg, wParam, lParam) {
				if(lParam){
				 	var text = ..raw.str( topointer(lParam) ); 
				 	if( text != this[["(text)"]] ){
				 		this[["(text)"]]  = text;
						this.redrawTransparent();
					}
				}
			} 
			[ 0x84/*_WM_NCHITTEST*/ ] = function (hwnd, uMsg, wParam, lParam) {
				if( !this.notify ) return 0xFFFFFFFF/*_HTTRANSPARENT*/;
				return 1/*_HTCLIENT*/
			}
			[ 0x87/*_WM_GETDLGCODE*/ ] = function (hwnd, uMsg, wParam, lParam) {  
				return this.notify? 0x10/*_DLGC_DEFPUSHBUTTON*/  : 0x100/*_DLGC_STATIC*/;
			} 
			[ 0x20/*_WM_SETCURSOR*/ ] = function (hwnd, uMsg, wParam, lParam) {   
				 if( this.isHyperlink() ){
				 	::SetCursor( __cursor_hand ); 
				 	return true; 
				 }
			} 
		}

		if( this.notify ){ 
			this._tracker =  ..win.ui.tracker(this);
		}
	};
	_onDestroy = function(){
		var foreground = this[["(foreground)"]];
		var background = this[["(background)"]];
		this[["(foreground)"]] = null;
		this[["(background)"]] = null;
		
		if( background ? ( ! background.isCached() ) ){
			background.dispose();
		} 
		if( foreground ? ( ! foreground.isCached() ) ){
			foreground.dispose();
		}
		if( this.cacheBitmap ) {
			var cacheBitmap = this.cacheBitmap;
			this.cacheBitmap = null;
			
			..gdip.DeleteCachedBitmap(cacheBitmap); 
		}
		if( this[["(progress-clip-path)"]]) this[["(progress-clip-path)"]].delete();
		if( this["(font)"] ) this["(font)"].delete();   
	};
	@_metaProperty
}

namespace plus {

	_metaProperty = ..win.ui.ctrl.metaProperty( 
		_onStateChange = function(){  
			var setting = owner[["(skin-setting)"]];  
			if( setting ){
				var state = owner.state; 
				if( state.checked ) setting = setting[["checked"]] : setting;
				var bt  = setting[["background"]];
				var ft  = setting[["foreground"]];
				var ct =  setting[["color"]];
				
				
				var b,f,c;
				if( state.disabled ) {
					b = bt[["disabled"]]
					f = ft[["disabled"]]
					c = ct[["disabled"]]
				} 
				elseif( state.active ){
					b = bt[["active"]] || bt[["focus"]] || bt[["default"]]
					f = ft[["active"]] || ft[["focus"]] || ft[["default"]]
					c = ct[["active"]]  || ct[["focus"]] || ct[["default"]]
				}
				elseif( state.hover ){ 
					b = bt[["hover"]] || bt[["default"]]
					f = ft[["hover"]] || ft[["default"]]
					c = ct[["hover"]] || ct[["default"]] 
				} 
				elseif( state.focus ){
					b = bt[["focus"]] || bt[["default"]]
					f = ft[["focus"]] || ft[["default"]]
					c = ct[["focus"]]  || ct[["default"]]
				} 
				else {
					b = bt[["default"]]
					f = ft[["default"]]
					c = ct[["default"]]  
				} 
				if(bt && b) owner.setBackground(b,,false);
				if(ft && f) owner.setForeground(f,,false);
				owner.argbColor = c : ct.default; 
				if( owner.onStateChange ) owner.onStateChange();
				owner.redrawTransparent();
				return;
			}
			if( owner.onStateChange ) owner.onStateChange();
		};
		checked = {
			_get = function () {
				return owner.state.checked;
			}
			_set = function (v) {
				if(v!=owner.state.checked){
					if( v ? owner.radioGroup ){ owner.radioClick() }
					else owner.state.checked = v;
					owner._onStateChange();
				}
			}
		};
		skin = function(s){ 
			if( type(s) != type.table ) error("参数@1类型错误",2);
			if( s.group ) owner.radioGroup = s.group;
			
			if(owner[["(skin-setting)"]]){  
				..table.mixr(owner[["(skin-setting)"]],s);
				owner._onStateChange();
				return;
			}
			
			if( s.background ) {
				if( ! s.background.default ) { s.background.default = owner.getBackground(); }
				elseif(!owner[["(skin-setting)"]]){ //释放IDE设计器添加的包含图片
				    var old = owner.setBackground(s.background.default,,false);
				    if( old ? ( ! old.isCached() ) ) { old.dispose(); }
				} 
			};
			
			if( s.foreground ){ 
				if(! s.foreground.default ) { s.foreground.default = owner.getForeground(); }
				elseif(!owner[["(skin-setting)"]]){ //释放IDE设计器添加的包含图片
				    var old = owner.setForeground(s.foreground.default,,false);
				    if( old ? ( ! old.isCached() ) ) { old.dispose(); }
				} 
			}
				
			if( !s.color ) s.color = {};
			..table.mix( s.color,{ default = owner.argbColor;disabled = ::GetSysColor(0x11/*_COLOR_GRAYTEXT*/)| ( 0xFF << 24 ) } ); 
			
			if( s.checked ){
				if( type(s.checked) != type.table) error("checked字段必须是表对象",2); 
				if( !s.checked.color ) s.checked.color = {};
				if( !s.checked.color.default ) s.checked.color.default = s.color.default; 
			}
			owner[["(skin-setting)"]] = s;
			
			//开启[事件回调]属性
			if( !owner.notify ) {
				owner.notify = true;
				if(!owner._tracker){
					owner._tracker =  ..win.ui.tracker(owner); 
				}
			}
			
			if( owner.state.disabled ){
				owner._onStateChange(); 
			}
		};
		text = {
			_get = function(){ 
				return ..win.getText( owner[["hwnd"]] );
			}
			_set = function(v){ 
				..win.setText( owner[["hwnd"]],tostring(v) );  
			}
		}; 
		background = {
			_get = function () {
				return owner.getBackground();
			}
			_set = function (v) {
				owner.setBackground(v,,!owner[["initializing"]]);
			}
		}
		foreground = {
			_get = function () {
				return owner.getForeground();
			}
			_set = function (v) {
				owner.setForeground(v,, !owner[["initializing"]]); 
			}
		}
		repeat = {
			_get = function () {
				return owner.getRepeat();
			}
			_set = function (v) {
				owner.setRepeat(v, !owner[["initializing"]]); 
			}
		}
		getRepeat = function(){
			return owner[["(repeat)"]];
		};
		setRepeat = function(v,redraw=true){
			owner[["(repeat)"]] = v; 
			if( redraw ) owner.redrawTransparent();
		};
		foreRepeat = {
			_get = function () {
				return owner.getForeRepeat();
			}
			_set = function (v) {
				owner.setForeRepeate(v, !owner[["initializing"]]); 
			}
		}
		getForeRepeat = function(){
			return owner[["(foreRepeat)"]];
		};
		setForeRepeate = function(v,redraw=true){
			owner[["(foreRepeat)"]] = v; 
			if( redraw ) owner.redrawTransparent();
		}; 
		getBackground = function(){
			return owner[["(background)"]];
		};
		setBackground = function(bmp,cacheName,redraw=true){
			var background = owner[["(background)"]];
			if( bmp == background ) return;
			
			var old = background;
			if( bmp ){
				background = ..gdip.loadCachedBitmap(bmp,cacheName); 
				if( background ){ 
					if( old ) old.stopAnimation();
					if( owner.animation === null ||  (owner.animation) ){
						background.createAnimation(owner,owner.redrawTransparent);
					} 
					owner[["(background)"]] = background;
				} 
				else {
					return;
				} 
			}
			else {
				return;
			}
			
			if( redraw ) owner.redrawTransparent(); 
			return old;  
		};
		getForeground = function(){
			return owner[["(foreground)"]];
		};
		setForeground  = function(bmp,cacheName,redraw=true){
			var foreground = owner[["(foreground)"]];
			if( bmp == foreground ) return;
			
			var old = foreground;
			
			
			if( bmp ){
				foreground = ..gdip.loadCachedBitmap(bmp,cacheName); 
				if( foreground ){ 
					if( old ) old.stopAnimation();
					if( owner.animation === null ||  (owner.animation) ){ 
						foreground.createAnimation(owner,owner.redrawTransparent);
					}
					owner[["(foreground)"]] = foreground;
				}
				else {
					return;
				}
			}
			else {
				return;
			}
			
			if( redraw ) owner.redrawTransparent();
			return old;  
		}; 
		isHyperlink = function(){
			return ( owner.notify ? #owner[["(text)"]] ? ( !owner[["(background)"]] ) ? ( !owner[["(foreground)"]] ) );
		};
		progressPercentage  = {
			_get = function () { 
				if(!owner[["(progress-range)"]]) return 100;
				return ..math.min( 100, ..math.round( owner[["(progress-pos)"]] / owner[["(progress-range)"]],2) * 100 );
			}
			_set = function (v) {
				if(!owner[["(progress-range)"]]) {
					if( owner[["(progress-range)"]] === 0 ) return;
					error("请先设定进度区间",2);
				}
				if( v<1) v = 1;if(v>100)v=100;
				owner[["(progress-pos)"]] = owner[["(progress-range)"]] * ( v/100 );
				owner.redrawTransparent();
				if( owner[["onPosChanged"]] ) owner.onPosChanged( owner[["(progress-min)"]] + owner[["(progress-pos)"]] );
			}
		}; 
		progressPos = {
			_get = function () {
				if(!owner[["(progress-range)"]]) return 0;
				return owner[["(progress-min)"]] + owner[["(progress-pos)"]];
			}
			_set = function (v) {
				if( v === null ) error("进度值不能为空",2);
				if(!owner[["(progress-range)"]]) {
					if( owner[["(progress-range)"]] === 0 ) return;
					error("请先设定进度区间",2);
				}
				v = ..math.max(v, owner[["(progress-min)"]])
				owner[["(progress-pos)"]] = ..math.min( v - owner[["(progress-min)"]],owner[["(progress-range)"]]);
				owner.redrawTransparent();
				if( owner[["onPosChanged"]] ) owner.onPosChanged(owner[["(progress-min)"]] + owner[["(progress-pos)"]]);
			}
		}; 
		setTrackbarRange = function(min,max){
			if( type(min) != "number") error("参数@1不是有效的数值",2)
			if( type(max) != "number") error("参数@2不是有效的数值",2)
			
			owner.setProgressRange(min,max);
			owner.mode = "trackbar";
			
			var rc = owner.getClientRect(); 
			var w = rc.right - rc.left;
			var h = rc.bottom - rc.top; 
			
			if( w > h ){
				owner.onMouseDrag = function(wParam,lParam){
					var x = ..win.getMessagePos(lParam);
					var rc = owner.getClientRect(); 
					var min = owner[["(progress-min)"]];
					var range = owner[["(progress-range)"]]; 
					var p = owner.foreLeft + owner.foreRight/2;
					var pos = ..math.ceil( range * ( ( x - owner.paddingLeft - p ) 
						/ (rc.right - owner.paddingLeft - owner.paddingRight - p  ) ) );
						
					if( pos > range ) pos = range;
					elseif( pos < 1 ) pos = 1; 
					
					owner[["(progress-pos)"]] = pos;
					owner.redrawTransparent(); 
					if( owner[["onPosChanged"]] ) owner.onPosChanged(min + pos);
				}
			}
			else {
				owner.onMouseDrag = function(wParam,lParam){
					var x,y = ..win.getMessagePos(lParam);
					var rc = owner.getClientRect(); 
					var min = owner[["(progress-min)"]];
					var range = owner[["(progress-range)"]];
				 
					var bottom = rc.bottom - owner.paddingBottom - owner.foreBottom;  
					var p = owner.paddingTop + owner.foreTop/2;
					var pos = y < bottom - p ? ..math.ceil( range * ( ( bottom - y - owner.foreTop/2  ) / (bottom - p ) ) ) : 0;
					if( pos > range ) pos = range;
					elseif( pos < 1 ) pos = 1;
					
					owner[["(progress-pos)"]] = pos;
					owner.redrawTransparent(); 
					if( owner[["onPosChanged"]] ) owner.onPosChanged( owner[["(progress-min)"]] + pos );
				}
			}
			
			owner.onMouseDown = owner.onMouseDrag; 
			
			//开启[事件回调]属性
			if( !owner.notify ) {
				owner.notify = true;
				if(!owner._tracker) owner._tracker =  ..win.ui.tracker(owner);
			}
		};
		setPieRange = function(min,max){
			if( type(min) != "number") error("参数@1不是有效的数值",2)
			if( type(max) != "number") error("参数@2不是有效的数值",2) 
			
			import ..gdip.path;
			owner[["(progress-clip-path)"]] = ..gdip.path();
			owner.setProgressRange(min,max);
			owner.mode = "pie";
		};
		getProgressRange = function(){
			var min = owner[["(progress-min)"]];
			return min,min + owner[["(progress-range)"]];
		};
		setProgressRange = function(min,max){
			if( type(min) != "number") error("参数@1不是有效的数值",2)
			if( type(max) != "number") error("参数@2不是有效的数值",2)
			if( max < min ){
				var temp = min;min=max;max=temp;
			}
			if( min == max ) {
				if( min == 1 ) min = 0;
			}
			
			owner[["(progress-min)"]] = min;
			owner[["(progress-range)"]] = max -min;
			owner[["(progress-pos)"]] = 0;
			owner.mode = "progress";
		}; 
		stepProgress = function(i,redraw){
			if( i === null ) i =  1;
			if( redraw === null ) redraw = true;
			var p,r = owner[["(progress-pos)"]],owner[["(progress-range)"]]; 
			if(!r) {
				if( r === 0 ) return;
				error("请先设定进度区间",2);
			}
			p = p + i;
			var ret;
			if( p < 0 ) p = 0;
			elseif( p > r ) p = r;
			else ret = true;
			
			owner[["(progress-pos)"]] = p;
			if(redraw) owner.redrawTransparent(); 
			if( owner[["onPosChanged"]] ) owner.onPosChanged( owner[["(progress-min)"]] + p);
			return ret;
		};
		getFont = function(){
			if(owner["(font)"]){
				var graphics = ..gdip.graphics(owner);
				var lf = gdip.font().getLogFont(graphics);
				graphics.delete();
				return lf;
			} 
		};
		setFont = function(f,own = true ){
			if( type(f) == type.pointer ){
				var lf = LOGFONT(); 
				if( !::GetObject(f, ..raw.sizeof(lf), lf) ) return;
				if(own) ::DeleteObject(f);
				f = lf;
			}
			if(type(f) != type.table ) error("参数错误@1",2)
			if(!f._struct) f = ::LOGFONT(f);
			if( f.handle && own) f.deleteIndirect();
			
			if(f.color) {
				owner.color = f.color
				owner.argbColor = ..gdi.rgbReverse( f.color ) | ( 0xFF << 24 ) 
			} 

			var hdc = ::GetDC(owner.hwnd);
			owner[["(font)"]] = ..gdip.font( hdc,f ); 
			::ReleaseDC(owner.hwnd,hdc);  

			return true; 
		}; 
		setFontEx = function(font){
			if( type.rget(font) == "gdip.font" && font.getLogFont ){
				owner[["(font)"]] = font;
			}
		};
		getFontEx = function(){
			return owner[["(font)"]];
		};
	);
	
	__cursor_hand  = ::LoadCursor(,topointer(32649/*_IDC_HAND*/));
	__stringAlignment = { 
		top =0/*_GdipStringAlignmentNear*/ ;bottom = 2/*_GdipStringAlignmentFar*/ ;
		left =0/*_GdipStringAlignmentNear*/ ; right = 2/*_GdipStringAlignmentFar*/;
		center = 1/*_GdipStringAlignmentCenter*/;
	}	
	__className = ..win.registerClassEx( "AAPicturePlus2",{
		lpfnWndProc = ::DefWindowProc; //必须移除static的回调函数,否则读写text会内存错误
		style =  0x4083/*_CS_HREDRAW | _CS_VREDRAW | _CS_PARENTDC | _CS_GLOBALCLASS*/
		},"static" ); 
}

/***intellisense() 
!ui_ctrl_plus.notify = 是否启用事件回调\n设为false将不能响应鼠标按键等交互事件
!ui_ctrl_plus.setFontEx(.(字体) = 设置gdip.font字体\n字体由控件接管并负责释放
!ui_ctrl_plus.getFontEx() = 返回gdip.font字体\n注意字体生命期由控件管理\n!gdipfont.
!ui_ctrl_plus.textRenderingHint = @.textRenderingHint = _GdipTextRenderingHint__ ;
!ui_ctrl_plus.trimming = @.trimming = _GdipStringTrimming__ ;
!ui_ctrl_plus.isHyperlink() = 控件不设置背景、前景\n但设置了显示文本,并启用事件回调则该值返回真
!ui_ctrl_plus.checked = 该属性可用于切换选取状态
!ui_ctrl_plus.radioGroup = 用于指定单选按钮分组名称\n也可以在skin函数的group属性中指定该值
!ui_ctrl_plus.radioValue() = 单选模式下选中控件的文本
!ui_ctrl_plus.radioClick() = 单选模式下选中控件
!ui_ctrl_plus.progressPercentage = 进度条当前百分比
!ui_ctrl_plus.progressPos = 进度条当前值
!ui_ctrl_plus.setTrackbarRange( = 设置跟踪条范围,\n可使用onPosChanged事件获取当前位置
!ui_ctrl_plus.setTrackbarRange(.(最小值,最大值) = 需要首先指定前景图并指定为九宫格拉伸显示
!ui_ctrl_plus.setPieRange( = 设置扇形进度条范围
!ui_ctrl_plus.setPieRange(.(最小值,最大值) = 需要首先指定圆形背景、前景图\n并指定为center显示模式
!ui_ctrl_plus.setProgressRange( = 设置进度条范围
!ui_ctrl_plus.getProgressRange() = 返回进度条最小值,最大值
!ui_ctrl_plus.setProgressRange(.(最小值,最大值) = 需要首先指定前景图并指定为九宫格拉伸显示
!ui_ctrl_plus.stepProgress( = 增加进度条的值
!ui_ctrl_plus.stepProgress(.(步进单位,是否刷新) = 步进单位可以省略,默认为1\n参数@2可省略,默认为true\n进度条到尾部或头部则返回false,否则返回true
!ui_ctrl_plus.bkTop = 上,背景图\nexpand模式为切图坐标\n其他模式为边距
!ui_ctrl_plus.bkRight = 右,背景图\nexpand模式为切图坐标\n其他模式为边距
!ui_ctrl_plus.bkBottom = 下,背景图\nexpand模式为切图坐标\n其他模式为边距
!ui_ctrl_plus.bkLeft = 左,背景图\nexpand模式为切图坐标\n其他模式为边距
!ui_ctrl_plus.foreTop = 上,前景图\nexpand模式为切图坐标\n其他模式为边距\n不指定模式为偏移值,负数为右下角坐标,小数为剩余空间百分比
!ui_ctrl_plus.foreRight = 右,前景图\nexpand模式为切图坐标\n其他模式为边距
!ui_ctrl_plus.foreBottom = 下,前景图\nexpand模式为切图坐标\n其他模式为边距
!ui_ctrl_plus.foreLeft = 左,前景图\nexpand模式为切图坐标\n其他模式为边距\n不指定模式为偏移值,负数为右下角坐标,小数为剩余空间百分比
!ui_ctrl_plus.argbColor = ARGB字体颜色,支持半透明\n使用数值表示颜色分量:0xAARRGGBB\n注意与RGB的分量顺序是反过来的
!ui_ctrl_plus.setRepeat(.("模式",是否重绘) = 修改背景显示模式\n支持模式expand,strech,center,tile,scale
!ui_ctrl_plus.setForeRepeat(.("模式",是否重绘) = 修改前景显示模式\n支持模式expand,strech,center,tile,scale
!ui_ctrl_plus.repeat = 背景显示模式\n支持模式expand,strech,center,tile,scale\expand模式foreTop,foreRight,foreBottom,foreLeft为九宫格切图坐标\n其他模式为显示边距
!ui_ctrl_plus.foreRepeat = 前景显示模式\n支持模式expand,strech,center,tile,scale\n如果为null则foreLeft,foreRight为偏移坐标\expand模式foreTop,foreRight,foreBottom,foreLeft为九宫格切图坐标\n其他模式为显示边距
!ui_ctrl_plus.background = 背景图像\n指定一个路径或资源路径控件将自动缓存使用频繁的图像\n内存图像建议使用setBackground函数指定缓存名\n!gdipbitmap.
!ui_ctrl_plus.foreground = 前景图像\n指定一个路径或资源路径控件将自动缓存使用频繁的图像\n内存图像建议使用setForeground函数指定缓存名\n!gdipbitmap.
!ui_ctrl_plus.setBackground() = !gdipbitmap.
!ui_ctrl_plus.setForeground() = !gdipbitmap.
!ui_ctrl_plus.setBackground(.(背景图像,缓存名,刷新重绘) = 修改背景图像\n缓存名为可选参数,默认以路径为缓存名\n如果参数@1是图像数据则可以使用参数@2指定缓存名\n刷新重绘参数@3默认为true\n该函数设置成功返回原来的背景图
!ui_ctrl_plus.setForeground(.(背景图像,缓存名,刷新重绘) = 修改背景图像\n缓存名为可选参数,默认以路径为缓存名\n如果参数@1是图像数据则可以使用参数@2指定缓存名\n刷新重绘参数@3默认为true\n该函数设置成功返回原来的前景图
!ui_ctrl_plus.state.hover = 鼠标是否在控件上方\n注意直接修改该状态控件不负责重绘
!ui_ctrl_plus.state.focus = 是否已得到焦点\n注意直接修改该状态控件不负责重绘
!ui_ctrl_plus.state.active = 鼠标或键盘键按下状态\n注意直接修改该状态控件不负责重绘
!ui_ctrl_plus.state.disabled = 已禁用\n注意直接修改该状态控件不负责重绘 
!ui_ctrl_plus.state.checked = 是否选中状态\n注意直接修改该状态控件不负责重绘 
!ui_ctrl_plus.oncommand( id,event ) = @.oncommand = function( id,event ){
	__/*控件被单击*/
}
!ui_ctrl_plus.onPosChanged( pos ) = @.onPosChanged = function( pos ){
	__/*进度值变更*/
}
!ui_ctrl_plus.onDrawContent(graphics,rc) = @.onDrawContent = function(graphics,rc){
	__/*返回真取消绘制文本、前景等默认操作*/
}
!ui_ctrl_plus.onDrawString()= @.onDrawString = function(graphics,text,font,rectf,strformat,brush){
    __/*自定义输出文本,请不要删除传入参数中的GDI+对象*/
    graphics.drawString(text,font,rectf,strformat,brush);
}
!ui_ctrl_plus.OnMouseEnter = @.OnMouseEnter = function(wParam,lParam){
	__/*鼠标移入*/
}
!ui_ctrl_plus.onMouseLeave = @.onMouseLeave = function(wParam,lParam){
	__/*鼠标移出*/
}
!ui_ctrl_plus.onMouseHover = @.onMouseHover = function(wParam,lParam){
	__/*鼠标悬停*/
}
!ui_ctrl_plus.onMouseMove = @.onMouseMove = function(wParam,lParam){
	if( wParam & 0x1/*_MK_LBUTTON*/ ){
		__/*鼠标拖动*/
	}
}
!ui_ctrl_plus.onMouseDown = @.onMouseDown = function(wParam,lParam){
	__/*鼠标按下*/
} 
!ui_ctrl_plus.onMouseClick = @.onMouseClick = function(wParam,lParam){
	__/*鼠标单击*/
}
!ui_ctrl_plus.onKeyDown = @.onKeyDown = function(keyCode,lParam,repeat){
	__/*按下键盘键*/
}
!ui_ctrl_plus.onKeyUp = @.onKeyUp = function(keyCode,lParam){
	__/*放开键盘键*/
}
!ui_ctrl_plus.onFocusGot = @.onFocusGot = function(wParam,lParam){
	__/*得到焦点*/
}
!ui_ctrl_plus.onFocusLost = @.onFocusLost = function(wParam,lParam){
	__/*失去焦点*/
}
!ui_ctrl_plus.onStateChange = @.onStateChange = function(){
	__/*状态已改变*/
}
!ui_ctrl_plus.skin = @.skin(\n     background = { \n         hover = "/res/images/button-hover.png";\n         focus = "/res/images/button-focus.png";\n         active = "/res/images/button-active.png";\n         disabled = "/res/images/button-disabled.png"; \n     }\n    color = {\n        hover = 0xF00000FF;__/*0xAARRGGBB A为透明度,R为红色分量,G为绿色分量,B为蓝色分量*/ \n    } \n) 
!ui_ctrl_plus.cls = 设计时类名
!ui_ctrl_plus.className = 运行时类名
!ui_ctrl_plus.hwnd = 控件句柄
!ui_ctrl_plus.id = 控件ID
!ui_ctrl_plus.text = 控件文本
!ui_ctrl_plus.getForm() = 如果是窗体返回自身\n如果是控件则返回_parentForm\n!winform.
!ui_ctrl_plus.isForm = 窗体返回true,控件返回false
!ui_ctrl_plus._parentForm = 控件所在的父窗口(指win.form对象)\n!winform.
!ui_ctrl_plus.getParent() = 返回父窗口\n!ui_ctrl_plus.
!ui_ctrl_plus.setParent(__/*控件对象*/) = 改变父窗口 
!ui_ctrl_plus.addCtrl = @.addCtrl(\n	button={ cls="button";text="button";left=33;top=32;right=126;bottom=81;autoResize=false }  \n)
!ui_ctrl_plus.translateAccelerator = @.translateAccelerator = function(msg){ \n	__/*返回是否快捷键*/\n} 
!ui_ctrl_plus.adjust = @.adjust = function( cx,cy,wParam ) {	 \n	__ \n};  
!ui_ctrl_plus.adjust() = 调整窗口 - 自定义事件函数
!ui_ctrl_plus._embedObject = 嵌入OLE控件对象\n!embedObject.
!ui_ctrl_plus.createEmbed(.("类名",容器对象) = 嵌入OLE控件,\n容器对象哦可选参数,返回容器对象,
!ui_ctrl_plus.createEmbed() = !embedObject.
!ui_ctrl_plus.hide = 控件是否隐藏
!ui_ctrl_plus.disabled = 是否禁用
!ui_ctrl_plus.left = 左侧坐标
!ui_ctrl_plus.right = 右侧坐标
!ui_ctrl_plus.top = 顶部坐标
!ui_ctrl_plus.bottom = 底部坐标
!ui_ctrl_plus.width = 宽度
!ui_ctrl_plus.height = 高度 
!ui_ctrl_plus.setRedraw(false) = 禁止重绘
!ui_ctrl_plus.setRedraw(true) = 恢复重绘
!ui_ctrl_plus.redraw() = 刷新重绘\n不重绘背景,速度较快
!ui_ctrl_plus.redrawTransparent() = 刷新重绘\n如果控件添加了透明样式 _WS_EX_TRANSPARENT 则重绘窗口背景
!ui_ctrl_plus.show(true__) = 显示控件
!ui_ctrl_plus.getRect() = 控件区块位置(::RECT结构体)
!ui_ctrl_plus.getRect(true) = 控件屏幕区块位置(::RECT结构体)  
!ui_ctrl_plus.setRect(rc) = 设置控件区块位置(::RECT结构体)  
!ui_ctrl_plus.setRect(rc,true) = 设置控件屏幕区块位置(::RECT结构体)
!ui_ctrl_plus.getClientRect() =  控件客户区块位置(::RECT结构体)\n!rect.
!ui_ctrl_plus.getFont() = 返回控件LOGFONT字体\n!logfont.
!ui_ctrl_plus.setFont(__/*指定字体*/) = 指定LOGFONT字体对象,或逻辑字体句柄 
!ui_ctrl_plus.setFont(混入字体属性) = @.setFont(point=10;name="宋体");
!ui_ctrl_plus.theme = 外观主题,例如\nwinform.button.theme = "Explorer"\nwinform.button.theme = false
!ui_ctrl_plus.modifyStyle(.(remove,add) = 如果指定第三个参数，则使用此参数调用::SetWidnowPos 
!ui_ctrl_plus.modifyStyleEx(.(remove,add) = 如果指定第三个参数，则使用此参数调用::SetWidnowPos
!ui_ctrl_plus.capture = 是否捕获全局鼠标消息
!ui_ctrl_plus.close() = 关闭控件窗口
!ui_ctrl_plus.invalidate(__/*可选使用::RECT()对象指定客户区*/) = 使窗口绘图区无效
!ui_ctrl_plus.invalidate(__/*可选使用::RECT()对象指定客户区*/,0) = 使窗口绘图区无效\n不刷新背景
!ui_ctrl_plus.update() = 重绘invalidate函数指定的区块
!ui_ctrl_plus.setFocus() = 设置焦点 
!ui_ctrl_plus.setPos(.(x坐标,y坐标,宽,高,插入位置,参数) = 调整窗口位置或排序,所有参数可选\n同时指定x,y坐标则移动位置\n同时指定宽高则改变大小\n指定插入位置(句柄或_HWND前缀常量)则调整Z序
!ui_ctrl_plus.getPos() = 返回相对坐标,宽,高\nx,y,cx,cy=win.getPos(hwnd)
!ui_ctrl_plus.messageOnly() = 将窗口转换为message-only window\n该窗口不可见,仅用于消息分发
!ui_ctrl_plus.addtimer(.(间隔时间,回调函数) = 创建定时器\n间隔时间以毫秒为单位
!ui_ctrl_plus.settimer(.(定时器ID,间隔时间,回调函数) = 重新设置间隔时间或回调函数
!ui_ctrl_plus.orphanWindow(true) = 参数必须为true,\n使控件脱离原来的窗口,可以显示在父窗口外面，\n但仍然显示在原来的位置，继续如影随形的跟随父窗口移动或改变大小,\n控件原来的固定边距等参数仍然有效
!ui_ctrl_plus.killtimer(__/*定时器ID*/) = 删除定时器
?win.ui.ctrl.plus  =!ui_ctrl_plus.
win.ui.ctrl.plus() = 高级图像控件\n!ui_ctrl_plus.
win.ui.ctrl.plus = 高级图像控件
end intellisense***/
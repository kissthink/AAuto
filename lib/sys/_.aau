//.. 系统操作
namespace sys;

lock = function(){
	return ::User32.LockWorkStation();
} 

shutdown = function( flags ){
    if(! setprivilege("SeShutdownPrivilege",true) ) return false;
    if( flags === null ) flags = 0xd /*_EWX_SHUTDOWN | _EWX_POWEROFF | _EWX_FORCE*/
    if( type(flags) != type.number ) error("参数错误",2);
    return !!::User32.ExitWindowsEx(flags,null);
}

restart = function(){
	return shutdown(6/*_EWX_REBOOT|_EWX_FORCE*/)
} 

logoff = function(){
	return shutdown(4/*EWX_LOGOFF|_EWX_FORCE*/)	
}

standby = function(){
	if(! setprivilege("SeShutdownPrivilege",true) ) return false;
	::Kernel32.SetSystemPowerState(1,1);
}

self["sleep"] = function(){
	if(! setprivilege("SeShutdownPrivilege",true) ) return false;
	::Kernel32.SetSystemPowerState(0,0);
}

getStartTime = function(){
	return ..time().addsecond(-(..time.tick()/1000)); 
}

::Advapi32 := ..raw.loadDll("Advapi32.dll") 
getUserName = function(){
	var getUserNameA =  Advapi32.api( "GetUserNameA", "bool(str& buf,int &size)");
	var ok,name = getUserNameA(0x101,0x101);	
	if(ok)return name;
}
getComputerName = function(){
	var getComputerNameA = ::Kernel32.api("GetComputerNameA","bool(str &lpBuffer,int& nSize)")
	var ok,name = getComputerNameA(32,32);	
	if(ok)return name;
}

setComputerName = function( name,ntype  ){
	if( ntype === null ) ntype = 5;
	if( type(name) != type.string ) error("参数@1未指定名称参数",2)
    return ::Kernel32.SetComputerNameEx(ntype,name)
}

/**intellisense(sys)
shutdown()=关机
restart()=重启
logoff()=注销
standby()=待机
sleep()=休眠
lock()=锁定计算机
getUserName() = 返回当前登录用户名
getComputerName() = 返回计算机名
setComputerName(.("计算机名") = 修改计算机名,重启后生效
getStartTime() = 返回系统启动时间\n!time.
end intellisense**/
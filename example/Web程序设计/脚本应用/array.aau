//使用JS数组
import win.ui;
/*DSG{{*/
var winform = ..win.form( bottom=249;scroll=1;right=349;text="AAuto Form";parent=...)
winform.add(  )
/*}}*/

import web.form;

//创建web窗体
var wb = web.form( winform 
    ,//可输入_UIFLAG_ 前缀的常量自定义外观
    ,//可输入_DLCTL_ 前缀的常量以控制下载行为
    ,//"USER AGENT"
    ,true);

//打开目标网站
wb.write("")
//显示窗体
winform.show() 
wb.wait("");//等待指定网址,可以使用模式匹配语法

import console;
wb.external={
	log = function(...){
		console.log(...) 
	}   
    [1] = "123";
    [2] = "456"; 
    item = function(index){
    	//external不能作为数组使用,
    	//所以我们需要自已实现一个接口访问数组成员
    	return owner[index+1] //注意AAuto下标从1开始,而Javascript从0开始
    } 
 }  
//在external中直接访问数组成员
var js = /* 
    external.log( external.item(1) ) 
    */
wb.doScript( js );

//======================================================

//枚举AAuto数组成员
var js = /* 
	var e = new Enumerator(external);   
	
	for   (;!e.atEnd();e.moveNext()){ 
		k = e.item(); 
		
		if( typeof(k)=="string")
			external.log(k,external[k]) 
	} 
    */
wb.doScript( js );

//======================================================
    
//通过返回值或参数交换数组
wb.external.items = function(index){
		return {123;"AAuto数组第二个成员"}
	}  
//在external中直接访问数组成员
var js = /*
	var aautoArray = external.items();
	var jsArray = new VBArray( aautoArray ).toArray() //将数组转换为js对象
	
    external.log( jsArray[1] ) 
    */
wb.doScript( js );

//======================================================

//使用js数组
wb.external.testJsArray = function(jsArray){

		io.open()
		
		for(i=1;jsArray.getlength() ){
			io.print( i, jsArray[i-1] ) 
		}
}  
//在external中直接访问数组成员
var js = /*
	var jsArray = new Array('abc',123)
	external.testJsArray(jsArray) 
    */
wb.doScript( js );

//======================================================

//进入消息循环
win.loopMessage();
io.close();//关闭控制台


import sqlServer;
import console;

//创建数据库
var db,err  = sqlServer( 
        server= ".\SQLEXPRESS";
        database= "数据库名";
        uid = "用户名";
        pwd= "用户密码"; 
)
assert(  db,err );

//创建表
if(!db.existsTable("homepage2") ){
	db.exec("create table homepage2 (url char(30), comment char(20))")
}

//使用命令参数 - 参数化可避免SQL注入
var cmd = db.createCommand( "INSERT INTO homepage2(url,comment) VALUES(@url,@comment)" );  
cmd.bind("homepage2").parameters( 
	url = "http://www.ecranesoft.com";
	comment = "hi!";
)
cmd.Execute() //执行命令

//增 - 调用 access.formatParameter() 格式化命名参数生成SQL语句
db.exec( "INSERT INTO homepage2(url,comment) VALUES(@url,@comment)",{
	url = "http://www.aauto.net";
	comment = "字符串包含'单引号' 测试一下"
} )

//删 - 自动调用 string.format() 函数格式化SQL语句,简单拼接字符串应避免包含单引号
db.exec("delete from homepage2 where url='%s'","http://www.ecranesoft.com");

//改
db.exec("update homepage2 set url='%s' WHERE comment='%s' ","http://www.aau.cn","这是说明")
 
//查
for(rs,fields in db.each("SELECT * from homepage2") ){ 
	console.log( rs("url").value,rs("comment").value )  
}
 
//将查询结果转换为普通数组
var tab = db.getTable("SELECT * FROM homepage2");
console.varDump(tab);

//关闭数据库连接
db.close();
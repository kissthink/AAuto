//扩展库
import ide;
import win.ui;
/*DSG{{*/
var winform = ..win.form(text="AAuto扩展库";right=665;bottom=399)
winform.add(
btnDeselectAll={cls="button";text="取消全选";left=432;top=362;right=511;bottom=391;db=1;dr=1;z=3};
btnSelectAll={cls="button";text="全选";left=334;top=362;right=413;bottom=391;db=1;dr=1;z=2};
btnSetup={cls="button";text="安装/更新";left=530;top=362;right=644;bottom=391;db=1;dr=1;z=4};
lbInfo={cls="static";text="鼠标双击上面的扩展库项目打开相关范例";left=21;top=366;right=316;bottom=394;db=1;dl=1;dr=1;transparent=1;z=5};
listview={cls="listview";left=11;top=12;right=658;bottom=356;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;items={};z=1}
)
/*}}*/

winform.listview.setExtended(0x4/*_LVS_EX_CHECKBOXES*/) 
winform.listview.insertColumn("扩展库",135) 
winform.listview.insertColumn("版本",70) 
winform.listview.insertColumn("本地版本",70) 
winform.listview.insertColumn("作者",50)  
winform.listview.insertColumn("说明",-1) 

winform.show() 
winform.text = "AAuto扩展库安装程序 正在获取服务器列表...."
 
var installedLibs = ide.getExtLibs();  
var listLib = ide.requestLibInfo("all")
if( listLib ){ 
	for libName,libInfo in table.eachName(listLib){ 
		var lib = listLib[libName]
		var row = winform.listview.addItem( { libName;libInfo.version;installedLibs[libName][["version"]] : "未安装";libInfo.author;libInfo.description } )
		if( installedLibs[libName][["version"]]  )
			winform.listview.setChecked(row,true)
	} 
}
else {
	winform.text = "AAuto扩展库安装程序 x"  +  ( err:"")
}

winform.text = "AAuto扩展库 - 已获取服务器最新列表";
winform.setTimeout( function(){
	winform.text = "AAuto扩展库 - 双击下面的列表项打开相关示范代码";
},1000 )


winform.btnSetup.oncommand = function(id,event){
	winform.btnSetup.disabled = true;
	
	for(i=winform.listview.count;1;-1){
		if( winform.listview.getChecked(i) ){
			var libName = winform.listview.getItemText(i,1) ;
			var libVersion = winform.listview.getItemText(i,2) ;
			var installVersion = installedLibs[libName][["version"]];
			
			if( installVersion != libVersion ){
				winform.text = "AAuto扩展库安装程序 正在安装扩展库:" + libName;
				
				win.invoke(
					function(libName,installVersion){
						import ide;
						ide.importFile( libName,installVersion, "" )
					} ,libName,installVersion
				)
				
				if(  ..win.isClosed() ) return;
				 
				installedLibs = ide.getExtLibs(); 
				winform.listview.setItemText(installedLibs[libName][["version"]]:"安装失败",i,3);
				if( installedLibs[libName][["version"]] == libVersion ){
					winform.text = "AAuto扩展库安装程序 已安装扩展库:"  + libName;
				}
				else {
					winform.text = "AAuto扩展库安装程序 扩展库:"  + libName + " 安装失败!";
				}
				
			}
			else {
				winform.text = "AAuto扩展库安装程序 忽略已安装扩展库:" + libName;
			}
			
		}
	} 
	
	winform.text = "AAuto扩展库安装程序 安装已完成" ;
	ide.refreshStdLib();
	winform.btnSetup.disabled = false;
}

//取消全选
winform.btnDeselectAll.oncommand = function(id,event){
	for(i=winform.listview.count;1;-1){
		winform.listview.setChecked(i,false) 
	} 	
}

//全选
winform.btnSelectAll.oncommand = function(id,event){ 
	for(i=winform.listview.count;1;-1){
		winform.listview.setChecked(i,true) 
	}  
}
 
import ide;
import inet.downBox;
import process;
import zlib.unzip;
import fsys;
fsys.createDir( "/.sampleCode/");

var requestLibSample = function(libName){
	if( !libName ) error("请指定库名称参数",2);
	return ..win.invoke(
		function(libName){
			import web.rest.jsonLiteClient;
			var http = web.rest.jsonLiteClient();
			var libApi = http.declareApi("http://update.aau.cn/aauto/lib/sample/find/?{libName}")
			var listLib = libApi[libName].get();
			http.close();
			return listLib;
		},libName
	) 
};
winform.listview.onnotify = function(id,code,ptr){

    select(code) { 
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
    		var nm = winform.listview.getNotifyMessage(code,ptr)
    		if(winform.listview.selIndex){
        		var libName = winform.listview.getItemText(nm.iItem,1)
        		if(!libName) return;
        		 
				var libInfo = requestLibSample(libName)
				if( !libInfo ){
					winform.lbInfo.text = "抱歉,暂时没有找到 " + libName + " 扩展库有关的范例"
					..process.execute("http://update.aau.cn/search/?q=" + libName);
					return;
				}; 
				 
				if( libInfo.siteUrl ){
					..process.execute( libInfo.siteUrl );
				}
				elseif( libInfo.codeUrl ){
					var code,err = win.invoke(
						function(url){
							import inet.whttp;
							return inet.whttp().get(url)
						},libInfo.codeUrl
					)
					if(!code) return;
					var codePath = "/.sampleCode/" + libName + ".aau";
					string.save(codePath,code )
					winform.show( 0x6/*_SW_MINIMIZE*/ )
					ide.openDocument(codePath)
				}
				elseif( libInfo.zipUrl ){
					var codePath = "/.sampleCode/" + libName + ".zip";
					var downBox = inet.downBox(winform,"/.sampleCode/" + libName,true )
	
					if( downBox.download(libInfo.zipUrl , codePath ) ){
	 					var ok,err = zlib.unzip.extract( codePath, "/.sampleCode/" + libName ) 
	 					..process.explore("/.sampleCode/" + libName)
	 				} 
				}
				elseif( libInfo.codePath ){
					ide.openDocument(  libInfo.codePath )
				}
				else{
					winform.lbInfo.text = "抱歉,暂时没有找到与 " + libName + "扩展库有关的范例"
					..process.execute("http://update.aau.cn/search/?q=" + libName);
					return;
				};
        	}
    	}
    }
}

win.loopMessage();
